subprojects {

	sourceCompatibility = 1.7
	group = 'com.github.hamsterksu'
	version = '1.1.0'
	
    apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'signing'

    repositories {
       mavenCentral()
    }

    dependencies {
    }
	
	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from 'build/docs/javadoc'
	}
 
	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}
	
	artifacts {
		archives jar
		archives javadocJar
		archives sourcesJar
	}
	
	signing {
		sign configurations.archives
	}

    jar {
        manifest.attributes provider: 'gradle'
    }
	
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	 
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}
	 
				pom.project {
				   name 'android-appversion-gradle-plugin'
				   packaging 'jar'
				   description 'Add extra information to output file name. Add build number to the application version.'
				   url 'https://github.com/hamsterksu/android-appversion-gradle-plugin'
	 
				   scm {
					   url 'scm:git@github.com:hamsterksu/android-appversion-gradle-plugin.git'
					   connection 'scm:git@github.com:hamsterksu/android-appversion-gradle-plugin.git'
					   developerConnection 'scm:git@github.com:hamsterksu/android-appversion-gradle-plugin.git'
				   }
	 
				   licenses {
					   license {
						   name 'The Apache Software License, Version 2.0'
						   url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						   distribution 'repo'
					   }
				   }
	 
				   developers {
					   developer {
						   id 'hamsterksu'
						   name 'Gennadiy Dubina'
					   }
				   }
			   }
			}
		}
	}
}